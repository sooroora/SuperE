Shader "UI/UIColorOverlay"
{
    Properties
    {
        [PerRendererData] _MainTex ("Sprite Texture", 2D) = "white" {}
        _Color ("Overlay Color", Color) = (1,1,1,1)
        _OverlayStrength ("Overlay Strength", Range(0,1)) = 1
    }

    SubShader
    {
        Tags
        {
            "Queue" = "Transparent"
            "IgnoreProjector" = "True"
            "RenderType" = "Transparent"
            "PreviewType" = "Plane"
            "CanUseSpriteAtlas" = "True"
        }

        Cull Off
        Lighting Off
        ZWrite Off
        Blend SrcAlpha OneMinusSrcAlpha

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"

            struct appdata_t
            {
                float4 vertex : POSITION;
                float2 texcoord : TEXCOORD0;
                float4 color : COLOR;
            };

            struct v2f
            {
                float4 vertex : SV_POSITION;
                float2 texcoord : TEXCOORD0;
                fixed4 color : COLOR;
            };

            sampler2D _MainTex;
            float4 _MainTex_ST;
            fixed4 _Color;
            float _OverlayStrength;

            v2f vert (appdata_t v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.texcoord = TRANSFORM_TEX(v.texcoord, _MainTex);
                o.color = v.color * _Color; // UI Image의 color와 곱
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                fixed4 baseCol = tex2D(_MainTex, i.texcoord);
                fixed4 blendCol = i.color;

                fixed3 overlayResult;

                // Photoshop Overlay 방식
                overlayResult = lerp(
                    baseCol.rgb,
                    (baseCol.rgb < 0.5) ?
                        (2.0 * baseCol.rgb * blendCol.rgb) :
                        (1.0 - 2.0 * (1.0 - baseCol.rgb) * (1.0 - blendCol.rgb)),
                    _OverlayStrength
                );

                fixed4 finalCol = fixed4(overlayResult, baseCol.a * blendCol.a);
                return finalCol;
            }
            ENDCG
        }
    }
}